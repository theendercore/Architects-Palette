plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.5"))

    api "fuzs.forgeconfigapiport:forgeconfigapiport-common-neoforgeapi:${config_port_version}"
    // compile against the JEI API but do not include it at runtime
    compileOnlyApi "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"

    compileOnly "dev.emi:emi-xplat-mojmap:${emi_version}:api"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
